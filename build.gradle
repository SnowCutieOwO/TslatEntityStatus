plugins {
	id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.7"
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'maven-publish'

	java.toolchain.languageVersion = JavaLanguageVersion.of(17)
	java.withSourcesJar()

	repositories {
		mavenCentral()

		maven {
			name = 'Sponge / Mixin'
			url = 'https://repo.spongepowered.org/repository/maven-public/'
		}

		maven {
			name = "NeoForge"
			url = 'https://maven.neoforged.net'
		}

		exclusiveContent {
			forRepository {
				maven {
					name = "Modrinth"
					url = "https://api.modrinth.com/maven"
				}
			}

			filter {
				includeGroup "maven.modrinth"
			}
		}
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.encoding = 'UTF-8'
		it.options.release = 17
	}

	tasks.withType(GenerateModuleMetadata) {
		enabled = false
	}

	publishing {
		repositories {
			maven {
				name = "Cloudsmith"
				url 'https://maven.cloudsmith.io/tslat/tes/'
				credentials {
					username System.getenv("CLOUDSMITH_USERNAME")
					password System.getenv("CLOUDSMITH_API_KEY")
				}
			}
		}
	}
}
